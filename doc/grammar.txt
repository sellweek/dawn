program = 'program' program-ident ';' [const-decl] [var-decl] {function} statement '.'

const-decl = 'const' ident '=' literal ';' {ident '=' literal ';'}
var-decl = 'var' ident {',' ident} ':' type-name ';' {ident {',' ident} ':' type-name ';'}

function = function-decl ('forward' | [var-decl] statement) ';'
function-decl = 'function' function-ident '(' [var-ident ':' type-name { ',' var-ident ':' type-name}] ')' [':', type-name] ';'

statement =
    var-ident ':=' expression   |
    'if' expression 'then' statement ['else' statement] |
    'while' expression 'do' statement |
    'for' var-ident ':=' expression for-operator expression 'do' statement |
    compound-statement |
    'exit' |
    function-call

expression =
    identifier-expression |
    literal |
    ('-' | 'not') expression |
    '(' expression ')' |
    expression ('<' | '<=' | '>' | '>=' | '=' | '<>' | '+' | '-' | 'mod' | '*' | 'div' | 'and' | 'or') expression

identifier-expression =
    var-ident |
    function-ident '(' [expression {',' expression}] ')'

function-call = function-ident '(' [expression {',' expression}] ')'

compound-statement = 'begin' {statement ';'} 'end'

for-operator = 'to' | 'downto'
program-ident = ident
var-ident = ident | [ '[' expression ']' ]
function-ident = ident
type-name = 'integer' | 'array' '[' expression '..' expression ']' 'of' type-name
array-bound = [ '-' ] number | ident
literal = number
